plugins {
    id "fabric-loom"
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "com.matthewprenger.cursegradle"
}

loom {
    accessWidener(file("src/main/resources/architectury.accessWidener"))
}

configurations {
    shadow
}

dependencies {
    minecraft("com.mojang:minecraft:${rootProject.architect.minecraft}")
    mappings(minecraft.officialMojangMappings())
    modCompile("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")
    modCompile("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")
    modCompileOnly("io.github.prospector:modmenu:${rootProject.mod_menu_version}")
    implementation "net.jodah:typetools:0.6.2"
    shadow "net.jodah:typetools:0.6.2"

    compileOnly(project(path: ":common", configuration: "transformed")) {
        transitive = false
    }
    runtimeOnly(project(path: ":common", configuration: "transformed")) {
        transitive = false
    }
    shadow(project(path: ":common", configuration: "transformed")) {
        transitive = false
    }
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    inputs.property "version", project.version
}

shadowJar {
    relocate "net.jodah.typetools", "me.shedaniel.architectury.shadowed.impl.net.jodah.typetools"
    configurations = [project.configurations.shadow]
    classifier "shadow"
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    archiveClassifier = "fabric"
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifact(remapJar.archivePath) {
                builtBy build
                classifier "fabric"
            }
        }
    }
}

curseforge {
    if (project.hasProperty("curse_api_key") || System.getenv("curse_api_key") != null) {
        apiKey = project.hasProperty("curse_api_key") ? project.property("curse_api_key") : System.getenv("curse_api_key")
        project {
            id = "419697"
            releaseType = "release"
            changelogType = "html"
            changelog = releaseChangelog()
            addGameVersion "1.16-Snapshot"
            addGameVersion "1.16.4"
            addGameVersion "Java 8"
            addGameVersion "Fabric"
            relations {
                requiredDependency "fabric-api"
            }
            mainArtifact(remapJar.archivePath) {
                displayName = "[Fabric $rootProject.supported_version] v$project.version"
            }
            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
    }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}

rootProject.tasks.getByName("curseforgePublish").dependsOn curseforge